apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'org.jreleaser'
apply plugin: 'signing'

ext {
    versionMajor = 1
    versionMinor = 0
    versionPatch = 0
    artifactVersion = "${versionMajor}.${versionMinor}.${versionPatch}"
}

android {
    namespace 'io.boxo.stripe'
    compileSdk 35

    defaultConfig {
        minSdk 21
        targetSdk 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}
dependencies {
    implementation libs.androidx.appcompat
    implementation libs.stripe.android
    implementation libs.boxoSdk
}
android {
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId 'io.boxo.sdk'
            artifactId 'boxo-stripe'

            pom {
                name = 'boxo-stripe'
                description = "Connect Boxo SDK with Stripe"
                url = "https://boxo.io"

                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }

                developers {
                    developer {
                        id = "boxo"
                        name = "Boxo Team"
                        email = "support@appboxo.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/Appboxo/android-boxo-stripe-sdk.git"
                    developerConnection = "scm:git:ssh://github.com:Appboxo/android-boxo-stripe-sdk.git"
                    url = "https://github.com/Appboxo/android-boxo-stripe-sdk"
                }

                withXml {
                    def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }

                afterEvaluate {
                    from(components["release"])
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    project {
        inceptionYear = "2025"
        author("@kaitkulov")
        version artifactVersion
    }
    gitRootSearch = true
    signing {
        active = 'ALWAYS'
        armored = true
        verify = true
    }
    release {
        github {
            skipTag = true
            sign = true
            branch = "master"
            branchPush = "master"
            overwrite = true
        }
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = "https://central.sonatype.com/api/v1/publisher"
                    stagingRepository(layout.buildDirectory.dir("staging-deploy").get().toString())
                    setAuthorization("Basic")
                    applyMavenCentralRules = false
                    sign = true
                    checksums = true
                    sourceJar = true
                    javadocJar = true
                    retryDelay = 60
                }
            }
        }
    }
}